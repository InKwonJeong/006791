apply plugin: "java"
apply plugin: "ivy-publish" // Ivy Publish 플러그인 적용

//...

group = "com.example"
version = 0.1

repositories {
  mavenCentral()
}

dependencies {
  runtime "org.apache.commons:commons-lang3:3.3.1"
}

task sourceJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allJava // 소스 세트를 입력 파일로 지정
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    mod1(IvyPublication) { // <mod1>는 임의의 모듈 식별명
      from components.java // 소프트웨어 컴포넌트 'java'를 공개 대상으로 지정
      // artifact() 메서드로 공개할 압축 파일이나 파일을 지정
      artifact(sourceJar) { // 태스크명을 지정하면 해당 태스크에 의해 출력 파일이 공개된다
        type 'source'
      }
      artifact(javadocJar) {
        type 'doc'
      }
    }
  }
}

